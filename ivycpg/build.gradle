buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'

    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
//apply plugin: 'jacoco-android'

apply from: '../jacoco.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

//apply plugin: 'com.android.library'

def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

static def fetchRevisionNumber() {
    def result = "git rev-list HEAD --count".execute().text.trim() //unix
    if (result.empty) result = "PowerShell -Command git rev-list HEAD --count".execute().text.trim()
    //windows
    if (result.empty) throw new RuntimeException("Could not generate versioncode on this platform? Cmd output: ${result.text}")
    return result.toInteger()
}

def majorVersion = 2
def minorVersion = 142
def patchVersion = 0
def revNumber = fetchRevisionNumber()

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.ivy.sd.png.asean.view"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode "$minorVersion$patchVersion".toInteger()
        versionName "${majorVersion}.${minorVersion}.${patchVersion}.${revNumber}"
        multiDexEnabled true
        resConfigs "en", "in", "lo", "th", "vi", "tr", "ar", "es", "fr"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "BASE_URL", "\"${BASE_CPG_STAGING_URL}\"")
        buildConfigField("String", "TEST_USER_NAME", "\"${TEST_USER_NAME}\"")
        buildConfigField("String", "TEST_PASSWORD", "\"${TEST_PASSWORD}\"")
        buildConfigField("boolean", "IS_AUTO_LOGIN_ENABLED", IS_AUTO_LOGIN_ENABLED)
    }
    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError true
        // if true, only report errors
        ignoreWarnings true

        baseline file("lint-baseline.xml")

    }
    signingConfigs {
        release {

            def storePass = System.getenv('keystoreusername')
            def password = System.getenv('keystorepassword')
            def aliasName = System.getenv('keyalias')
            def keyStorePath = System.getenv('keystorepath')


            storeFile file("${keyStorePath}")
            keyAlias "${aliasName}"
            keyPassword "${password}"
            storePassword "${storePass}"
        }

        devrelease {

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            resValue "string", "google_maps_api_key", "AIzaSyBQucltx5i10fqfxaFQNpRb6igGsnmdk0Q"
            buildConfigField("boolean", "ACTIVATION", "false");

            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            testCoverageEnabled = true
            debuggable false

        }
        release {
            def googleapikey = System.getenv('googleapikey')
            resValue "string", "google_maps_api_key", "${googleapikey}"
            buildConfigField("boolean", "ACTIVATION", "true")


            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        devrelease {
            resValue "string", "google_maps_api_key", keystoreProperties['mapKey']
            buildConfigField("boolean", "ACTIVATION", "true")


            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.devrelease
            zipAlignEnabled true
        }
    }
    dexOptions {
        preDexLibraries = false
        jumboMode = true
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    sourceSets {
        aws {
            java.srcDirs = ['src/AWS/java', 'src/main/java']

        }
        sfdc {

            java.srcDirs = ['src/SFDC/java', 'src/main/java']
            res.srcDirs = ['src/SFDC/res', 'src/main/res']
        }

        main {
            // res.srcDirs = getLayoutList("ivycpg/src/main/res/layouts/")
            res.srcDirs = [
                    'src/main/res/layouts/dashboard',
                    'src/main/res/layouts/photocapture',
                    'src/main/res/layouts/activation',
                    'src/main/res/layouts/note',
                    'src/main/res/layouts/offline',
                    'src/main/res/layouts/retailer',
                    'src/main/res/layouts/announcement',
                    'src/main/res/layouts',
                    'src/main/res'
            ]
        }
    }


    flavorDimensions "cpg"
    productFlavors {
        aws {
            dimension "cpg"
        }
        sfdc {
            minSdkVersion 19
            dimension "cpg"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

static def getLayoutList(path) {
    // let's say path = "app/src/main/res/layouts/
    File file = new File(path)

    def throwAway = file.path.split("/")[0]
    // throwAway = 'app'

    //println throwaway
    println "${throwAway}"

    def newPath = file.path.substring(throwAway.length() + 1) // +1 is for '/'
    // newPath = src/main/res/layouts/

    def array = file.list().collect {
        // println "filename: ${it}" // uncomment for debugging
        "${newPath}/${it}"
    }

    array.push("src/main/res");
    // println "result: ${array}" // uncomment for debugging

    return array
}

dependencies {

    implementation project(':ivylibrary')

    implementation project(':calendarlibrary')

    //Networking
    implementation 'com.android.volley:volley:1.1.0'

    // Excel Generator
    implementation files('libs/jxl.jar')

    // Printer Library
    implementation files('libs/ZSDK_ANDROID_API.jar')
    implementation files('libs/BixolonPrinter.jar')
    implementation files('libs/tls.jar')
    implementation files('libs/AEM_SCRYBE_SEP_2014.jar')
    // Printer Library - Common Bluetooth
    implementation files('libs/deviceutility.jar')

    //BCrypt Password encryption
    implementation files('libs/jbcrypt-0.3.jar')

    // Baidu Maps
    implementation files('libs/baidumapapi_base_v3_6_1.jar')
    implementation files('libs/baidumapapi_map_v3_6_1.jar')
    implementation files('libs/baidumapapi_search_v3_6_1.jar')

    // Email library
    implementation files('libs/activation.jar')
    implementation files('libs/javax.mail-1.4.7.jar')

    // Support Library
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.0'


    //Google Play service and maps
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.4.4'

    // Firebase Library
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'
    implementation 'com.google.firebase:firebase-database:18.0.0'
    implementation 'com.google.firebase:firebase-auth:18.0.0'
    implementation 'com.google.firebase:firebase-firestore:20.1.0'

    // Image processing
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    // Barcode reading
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.mindorks.android:prdownloader:0.4.0'
    implementation 'com.elyeproj.libraries:loaderviewlibrary:1.5.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    // Butter Knife
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    // Dragger
    implementation 'com.google.dagger:dagger:2.13'
    annotationProcessor "com.google.dagger:dagger-compiler:2.13"
    implementation 'androidx.annotation:annotation:1.0.0'

    // Leak Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjava2Version"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"

    // Event Bus
    implementation "org.greenrobot:eventbus:$rootProject.eventBusVersion"

    //Chat-Sdk
    implementation 'co.chatsdk.chatsdk:chat-sdk-core:4.1.36'
    implementation 'co.chatsdk.chatsdk:chat-sdk-ui:4.1.36'
    implementation 'co.chatsdk.chatsdk:chat-sdk-firebase-adapter:4.1.29'
    implementation 'co.chatsdk.chatsdk:chat-sdk-firebase-file-storage:4.1.26'
    implementation 'co.chatsdk.chatsdk:chat-sdk-firebase-push:4.1.36'

    // dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.hamcrestVersion"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"
    testImplementation 'org.json:json:20140107'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.0RC2'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.0'
    testImplementation 'org.powermock:powermock-core:1.7.0'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.7.0'

    // Unit Test Frameworks
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'

    //SFDC SDK
    sfdcImplementation 'com.salesforce.mobilesdk:SmartSync:5.0.0'

    // Amazon S3
    implementation files('libs/aws-android-sdk-core-2.6.15.jar')
    implementation files('libs/aws-android-sdk-s3-2.6.15.jar')

    // Microsoft Azure Storage
    implementation 'com.microsoft.azure.android:azure-storage-android:0.8.0@aar'

    implementation "com.ivy.apptutoriallibrary:apptutoriallibrary:1.0.2"
    implementation "com.airbnb.android:lottie:2.7.0"
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.google.android.gms:play-services-places-placereport:15.0.1'


    implementation 'com.stepstone.stepper:material-stepper:4.3.1'
}
